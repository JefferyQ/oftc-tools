#!/usr/bin/perl

use strict;
use warnings;
use Monitoring::Livestatus;
use YAML::Syck;

print "Content-Type: image/png\n\n";

my $qs = $ENV{QUERY_STRING} || '';
my ($testnet, $ircdport, $drubyport) = $qs =~ /testnet/ ?
	("testnet-", 16667, 18787) : ("", 6667, 8787);

# Set this for interoperability with other YAML/Syck bindings:
# e.g. Load('Yes') becomes 1 and Load('No') becomes ''.
$YAML::Syck::ImplicitTyping = 1;
my $servers = LoadFile('/home/oftc/oftc-is/config/servers.yaml') or die "servers.yaml: $@";

my (%ircd, %host2ircd);

foreach my $host (@{ $servers->{servers} }) {
	die "server without name" unless ($host->{name});
	my $name = $host->{name};
	if ($host->{area}) {
		$ircd{$name}->{area} = $host->{area};
	}
	if ($host->{hub}) {
		$ircd{$name}->{hub} = 1;
	}
	if ($host->{nagioshost}) {
		$host2ircd{$host->{nagioshost}} = $name;
		$ircd{$name}->{nagioshost} = $host->{nagioshost};
	}
}

my $ml = Monitoring::Livestatus->new(
	socket => '/var/lib/icinga/rw/live'
);
my $query = "GET services
Columns: host_name description state plugin_output
Filter: description = network service - ${testnet}ircd - $ircdport
Filter: description = network service - ${testnet}ircd - login and users
Filter: description = network service - ${testnet}ircd - shedding
Or: 3
";
my $hosts = $ml->selectall_arrayref($query, { Slice => {} });

foreach my $row (@$hosts) {
	my $name = $host2ircd{$row->{host_name}} ? $host2ircd{$row->{host_name}} : $row->{host_name};
	if ($row->{description} eq "network service - ${testnet}ircd - $ircdport") {
		if ($row->{plugin_output} =~ /OK/) {
			$ircd{$name}->{irc} = 'OK';
		}
	} elsif ($row->{description} eq "network service - ${testnet}ircd - login and users") {
		if ($row->{plugin_output} =~ /(?:Current Local Users|Number Of Clients Connected ): (\d+)/) {
			$ircd{$name}->{users} = $1 - 1;
		}
	} elsif ($row->{description} eq "network service - ${testnet}ircd - shedding") {
		if ($row->{plugin_output} =~ /not shedding/) {
			$ircd{$name}->{shedding} = 0;
		} elsif ($row->{plugin_output} =~ /: shedding/) {
			$ircd{$name}->{shedding} = 1;
		}
	} else {
		die "unexpected description $row->{description}";
	}
}

open ROTATION, "/etc/oftc/dns/rotation" or die "rotation: $!";
while (<ROTATION>) {
	next if (/^\s*;/);
	if (/(\S+)\.region(6?).*?;\s*(\S+)/) {
		push @{ $ircd{$3}->{rotation} }, "$1$2";
	}
}
close ROTATION;

my $command = -t 1 ? "cat" : "fdp -Tpng";
open DOT, "| $command";

print DOT "graph oftc {\n";
foreach my $host (sort keys %ircd) {
	my $ircd = $ircd{$host};
	my $fillcolor = $ircd->{shedding} ? 'blue' :
		(! $ircd->{irc} ? 'grey' :
		(! $ircd->{rotation} ? 'yellow' : ''));
	printf DOT "  \"%s\" [label=\"%s%s\\n%s%s users%s%s%s%s\"%s,shape=\"box\"%s]\n",
		$host,
		$host, ($ircd->{nagioshost} ? " ($ircd->{nagioshost})" : ''),
		($ircd->{hub} ? 'hub, ' : ''),
		(exists $ircd->{users} ? $ircd->{users} : '??'),
		($ircd->{irc} ? '' : '\nnot listening'),
		($ircd->{area} ? "\\n$ircd->{area}" : ''),
		($ircd->{rotation} ? map { " $_" } @{$ircd->{rotation}} : ''),
		($ircd->{shedding} ? '\nshedding' : ''),
		($fillcolor ? ",style=filled,fillcolor=$fillcolor" : ''),
		($ircd->{hub} ? ',peripheries=2' : '');
}

my $links = "/var/cache/oftc/${testnet}links";
my $mtime = (stat $links)[9] || 0;
if ($mtime < time - 60) { # older than 1 minute
	system "nagios-get-links -p $drubyport > $links";
}

if (open LINKS, $links) {
	print DOT "\n";

	while (<LINKS>) {
		next unless (/([^. ]+)\S* ([^. ]+)\S*/);
		next if $1 eq $2;
		print DOT "  \"$1\" -- \"$2\"\n";
	}
}

print DOT "}\n";
close DOT;
