#!/usr/bin/perl

use strict;
use warnings;
use YAML::Syck; # last_run_summary.yaml doesn't have a trailing newline. YAML and YAML::Tiny don't like that

# time: 
#   file_line: 0.008641
#   config_retrieval: 1.80002188682556
#   total: 6.09638688682556
#   service: 1.078981
#   package: 0.007719
#   augeas: 2.285378
#   last_run: 1426959252
#   filebucket: 0.000151
#   file: 0.915495
# resources: 
#   failed_to_restart: 0
#   scheduled: 0
#   changed: 0
#   total: 42
#   restarted: 0
#   out_of_sync: 0
#   failed: 0
#   skipped: 6
# changes: 
#   total: 0
# events: 
#   failure: 0
#   total: 0
#   success: 0
# version: 
#   config: 1426959246
#   puppet: "2.7.23"

my $last_run_summary = '/var/lib/puppet/state/last_run_summary.yaml';
my ($yaml) = LoadFile($last_run_summary);
unless ($yaml) {
	print "$last_run_summary not found\n";
	exit 2;
}

my $warning = 0;
my $critical = 0;

my $last_run = scalar localtime($yaml->{time}->{last_run});
my $msg = "Last run: $last_run.";
my $age = time - $yaml->{time}->{last_run};
if ($age > 4800) {
	$critical = 1;
} elsif ($age > 2400) {
	$warning = 1;
}

if ($yaml->{resources}->{failed_to_restart} > 0) {
	$msg .= " $yaml->{resources}->{failed_to_restart} failed to restart.";
	$critical = 1;
}
if ($yaml->{resources}->{out_of_sync} > 0) {
	$msg .= " $yaml->{resources}->{out_of_sync} out of sync.";
	$critical = 1;
}
if ($yaml->{resources}->{failed} > 0) {
	$msg .= " $yaml->{resources}->{failed} failed.";
	$critical = 1;
}

print "$msg\n";
exit ($critical ? 2 : $warning);
